package fhir_r3r4

import "../ResourceBase.wstl"

// Description: Maps the common fields in FHIR STU3 and FHIR R4
//
// Argument(s):
//   medication : FHIR Medication resource
//
// Output(s):
//   FHIR Medication Resource
//
def MedicationBase(medication) {
  resourcebase::ResourceBase(medication)
  code: medication.code
  form: medication.form
}

// Description: Converts a FHIR STU3 Medication resource to FHIR R4 Medication resource.
//
// Argument(s):
//   medication : FHIR STU3 Medication resource https://www.hl7.org/fhir/stu3/medication.html
//
// Output(s):
//   FHIR R4 Medication Resource https://www.hl7.org/fhir/r4/medication.html
//
def R3MedicationToR4(medication) {
  MedicationBase(medication)
  contained: datatypes::R3ResourceToR4(medication.contained[])
  manufacturer: datatypes::R3ReferenceToR4(medication.manufacturer)
  identifier: extension::fetchIdentifierArray("https://www.hl7.org/fhir/R4/medication-definitions.html#Medication.identifier", medication.extension)
  status: medication.status
  amount: extension::fetchRatio("https://www.hl7.org/fhir/R4/medication-definitions.html#Medication.amount", medication.extension)
  ingredient: R3Medication_IngredientToR4(medication.ingredient[])
  batch: medication.'package'.batch[0]
  extension[]: extension::BuildAttachmentExtension("https://www.hl7.org/fhir/STU3/medication-definitions.html#Medication.image", medication.image[])
  extension[]: extension::BuildCodeableConceptExtension("https://www.hl7.org/fhir/STU3/medication-definitions.html#Medication.package.container", medication.'package'.container)
}

// Description: Converts a FHIR R4 Medication resource to FHIR STU3 Medication resource.
//
// Argument(s):
//   medication : FHIR R4 Medication resource https://www.hl7.org/fhir/r4/medication.html
//
// Output(s):
//   FHIR STU3 Medication Resource https://www.hl7.org/fhir/stu3/medication.html
//
def R4MedicationToR3(medication) {
  MedicationBase(medication)
  contained: datatypes::R4ResourceToR3(medication.contained[])
  manufacturer: datatypes::R4ReferenceToR3(medication.manufacturer)
  status: medication.status
  isBrand: medication.isBrand
  isOverTheCounter: medication.isOverTheCounter
  ingredient: R4Medication_IngredientToR3(medication.ingredient[])
  'package'.batch[]: medication.batch
  'package'.container: extension::fetchCodeableConcept("https://www.hl7.org/fhir/STU3/medication-definitions.html#Medication.package.container", medication.extension)
  image: extension::fetchAttachmentArray("https://www.hl7.org/fhir/STU3/medication-definitions.html#Medication.image", medication.extension)
  extension[]: extension::BuildIdentifierExtension("https://www.hl7.org/fhir/R4/medication-definitions.html#Medication.identifier", medication.identifier[])
  extension[]: extension::BuildRatioExtension("https://www.hl7.org/fhir/R4/medication-definitions.html#Medication.amount", medication.amount)
}

// Description: Converts a FHIR STU3 Medication.ingredient to FHIR R4 Medication.ingredient.
//
// Argument(s):
//   data : FHIR STU3 Medication.ingredient https://www.hl7.org/fhir/stu3/medication-definitions.html#Medication.ingredient
//
// Output(s):
//   FHIR R4 Medication.ingredient https://www.hl7.org/fhir/r4/medication-definitions.html#Medication.ingredient
//
def R3Medication_IngredientToR4(data) {
  itemCodeableConcept: data.itemCodeableConcept
  itemReference: datatypes::R3ReferenceToR4(data.itemReference)
  isActive: data.isActive
  strength: data.amount
}

// Description: Converts a FHIR R4 Medication.ingredient to FHIR STU3 Medication.ingredient.
//
// Argument(s):
//   data : FHIR R4 Medication.ingredient https://www.hl7.org/fhir/r4/medication-definitions.html#Medication.ingredient
//
// Output(s):
//   FHIR STU3 Medication.ingredient https://www.hl7.org/fhir/stu3/medication-definitions.html#Medication.ingredient
//
def R4Medication_IngredientToR3(data) {
  itemCodeableConcept: data.itemCodeableConcept
  itemReference: datatypes::R4ReferenceToR3(data.itemReference)
  isActive: data.isActive
  amount: data.strength
}
